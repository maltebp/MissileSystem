//=============================================================================================================================================

package Projectile

//=============================================================================================================================================

import LinkedList
import ClosureTimers

//=============================================================================================================================================
/*
    ABOUT
    A system to create custom projectiles with, using closures rather than inheritance (abstract classes).

    HOW TO USE:
    Construct a new projectile using any of the constructors, and fire it using any of the
    fire methods.

        - ADD EFFECT (such as damage) by using the addActionOnFinish() method
        - ADD COLLSION aoe collision by using the addCollisionAction() method
        
*/
//=============================================================================================================================================
public class Projectile

    protected effect projectileSfx

    private vec3 pos
    private vec3 velocity // Directed speed
    private real speed // Total speed
    private real direction
    

    private vec3 targetPos
    private unit targetUnit = null
    private real targetZOffset = 0
     
    // Toggleable
    private var pitchEnabled    = true
    private var destroyOnFinish = true
    private var shouldStop = false

    // Other bools
    private boolean finished        = false
    private boolean arcEnabled      = false
    private boolean homingEnabled   = false

    // Remote Functions
    private ProjectileUnitFilter    collisionFilter = null
    private ProjectileUnitAction    collisionAction = null
    
    private LinkedList<ProjectileAction>   actionsOnFinish = null
    private ProjectileAction               action_preUpdate = null

    private var collisionRange = 0.
    private var collideOnce = true
    private group collidedUnits = null

    // Using temp group, since someone else might be using enum group
    private static group tempGroup = CreateGroup()

    private real updateFreq = ANIMATION_PERIOD
    private CallbackPeriodic updateCallback = null
    private CallbackSingle timedLifeCallback = null 


    // ===============================================================

    /** 
        Constructs a new projectile, which may be fired with any of the "fire" methods (i.e. 'firePoint()).
            originPos:  The position at which to create the projectile. Adjust z-coordinate of this to adjust start height of the projectile.
            sfxPath:    The model path for the projectile
    */
    construct( vec3 originPos, string sfxPath )
        pos = originPos
        projectileSfx = addEffect(sfxPath, pos)

        
    /**  Constructs a "hidden" projectile object without a model. May be fired with any of the "fire" methods (i.e. 'firePoint()). */
    construct( vec3 originPos )
        pos = originPos


    /**
        Fires the projectile towards a target unit. 
            targetZOffset:  Vertical offset the projectile will hit. No offset will make it strike the unit's origin.
            homing:     Makes the projectile chase the unit. If not enabled the projectiles target will be the unit's position.
            speed:      The speed of the projectile (range per second). Same factor as original WC3 projectile.
            arc:        The arc of the projectile. Same factor as original WC3 projectiles (not radians nor degrees).
    */
    function fireUnit(unit targetUnit, real targetZOffset, boolean homing, real speed, real arc)
        initialize( targetUnit.getPos3Real(), speed, arc)

        homingEnabled = homing
        this.targetUnit = targetUnit
        this.targetZOffset = targetZOffset


    /**
        Fires the projectile towards a point.
            targetPos:  The target point. The target z offset may be adjusted with this.
            speed:      The speed of the projectile (range per second). Same factor as original WC3 projectile.
            arc:        The arc of the projectile. Same factor as original WC3 projectiles (not radians nor degrees).
    */
    function firePoint( vec3 targetPos, real speed, real arc )
        initialize( targetPos, speed, arc )
        // if not arcEnabled
        //     timedLifeCallback = doAfter( pos.distanceTo2d(targetPos)/speed ) ->
        //         finish()
        

    // TODO: Implement this
    // /**
    //     Fires the projectile towards a point but for a set distance. Use addCollisionAction() and addCollisionFilter() to add
    //     collision detection and effect.
    //         targetPos:  The target point to fire towards (may not necessarily hit this point)
    //         distance:   A specific distance the projectile will travel
    //         speed:      The speed of the projectile (range per second). Same factor as original WC3 projectile.
    // */
    // function fireLine( vec3 targetPos, real distance, real speed )
    //     initialize( targetPos, speed, 0 )
    //     // timedLifeCallback = doAfter( distance/speed ) ->
    //     //     finish()

    /**
        Stop the projectile before it was actually meant to (i.e. before it has reached its end point).
        Use this instead of 'destroy' if you want to stop the projectile preemptively. */
    function stop()
        shouldStop = true
        

    //------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------

    ondestroy

        if( updateCallback != null )
            destroy updateCallback

        if( timedLifeCallback != null )
            destroy timedLifeCallback

        if actionsOnFinish != null
            for actionOnFinish in actionsOnFinish
                destroy actionOnFinish
            destroy actionsOnFinish

        if collisionFilter != null
            destroy collisionFilter

        if collisionAction != null
            destroy collisionAction

        if collidedUnits != null
            collidedUnits.destr()

        projectileSfx
        ..setYaw(0)
        ..setPitch(0)
        ..destr()


    /* Initializes the projectile with some generic things, such as calculating a starting velocity,
        the actual speed (range per ANIMATION_PERIOD) etc.
    */
    private function initialize( vec3 targetPos, real speed, real arc )
        this.targetPos = targetPos
        this.speed = speed * ANIMATION_PERIOD
        this.direction = pos.angleTo2d(targetPos).radians()
        finished = false

        arcEnabled = arc > 0

        real zVelocity

        if( arcEnabled )
            // this.speed*4*arc is the adjustment which gives it the WC3 like arc
            zVelocity = this.speed*4*arc  
           
        else
            let distance = targetPos.distanceTo2d(pos)
            let time = distance / this.speed
            zVelocity = (targetPos.z - pos.z) / time
            

        velocity = vec3( Cos(direction) * this.speed,
                        Sin(direction) * this.speed,
                        zVelocity )

        if pitchEnabled
            let angle = Atan( velocity.z / this.speed )
            projectileSfx.setPitch(-angle*Cos(direction))
            projectileSfx.setYaw(angle*Sin(direction))
            
        projectileSfx.setRoll( direction )

    
        // Start Update
        updateCallback = doPeriodically(updateFreq) cb ->
            if( shouldStop )
                finish()
            else
                update()    

        


    protected function update()

        if action_preUpdate != null
            action_preUpdate.run()

        // The method is split into different sections in order to optimize the run speed.
        
        let remainingDistance = pos.distanceTo2d(targetPos.toVec2())
        if speed >= remainingDistance
            finish()
            return

        if homingEnabled
            let remainingTime = remainingDistance / speed

            this.targetPos = this.targetUnit.getPos().withTerrainZ(targetZOffset)
            this.direction = this.pos.angleTo2d(this.targetPos).radians()
        
            this.velocity.x = Cos(this.direction) * this.speed
            this.velocity.y = Sin(this.direction) * this.speed

            // TEMPORARY SOLUTION!4
            if not arcEnabled
                this.velocity.z = (targetPos.z - pos.z) / remainingTime

            this.projectileSfx.setRoll(this.direction)

            if pitchEnabled
                let angle = Atan( velocity.z / speed )
                projectileSfx.setPitch(-angle*Cos(direction))
                projectileSfx.setYaw(angle*Sin(direction))

        if arcEnabled
            let heightDif =  targetPos.z - pos.z
            let time = remainingDistance / speed
            let gravity = (2 / time) * (heightDif / time - velocity.z)

            velocity.z += gravity

            if pitchEnabled
                let angle = Atan( velocity.z / speed )
                projectileSfx.setPitch(-angle*Cos(direction))
                projectileSfx.setYaw(angle*Sin(direction))
            
            
        // Update position
        pos += velocity
        projectileSfx.setPos(pos)

        // Check collision
        if( collisionAction != null )
            tempGroup..clear()..enumUnitsInRange( pos.toVec2(), collisionRange )
            
            for target from tempGroup
                if( shouldStop )
                    finish()
                    return
                if( (not collideOnce or not collidedUnits.contains(target)) and (collisionFilter == null or collisionFilter.filter(target)) )
                    collidedUnits.addUnit(target)
                    collisionAction.run(target)




    private function finish()
        // Stopping update
        if( updateCallback != null )
            destroy updateCallback
            updateCallback = null

        shouldStop = destroyOnFinish

        if actionsOnFinish != null
            for action in actionsOnFinish
                action.run()

        /* An actionOnFinish action might tell it to stop,
            even though its set to not destroy on finish.
            therefor wecheck it here again. */
        if( shouldStop )
            destroy this


    /**
        Adds an action to run once the projectile has reached it target set by the used 'fire' method, like damage, bounce etc.
        You may add several pieces of code to run using this.
    */
    function addActionOnFinish( ProjectileAction action )
        if actionsOnFinish == null
            actionsOnFinish = new LinkedList<ProjectileAction>
        actionsOnFinish.add(action)



    /**
        Activate collision detection for the projectile, and run custom action. To add a filter, use the setCollisionFilter() method.
        You may also just do the filtering within the action you're implementing. 
        To make the projectile able to hit same targets again, use the clearCollidedUnits() method. 
            range:          The range at which to detect units
            collideOnce:    False will make it collide with the unit every ANIMATION_PERIOD (~0.03 seconds)
            action:         The action to run once a target is hit */
    function setCollisionAction( real range, boolean collideOnce, ProjectileUnitAction action )

        // Setup collision group
        if( collideOnce and collidedUnits == null )
            collidedUnits = CreateGroup()

        collisionAction = action
        collisionRange = range
        this.collideOnce = collideOnce                   
                


    /**
        Makes the projectile detect collision with units. To add a filter, use the setCollisionFilter() method.
            range:          The range at which to detect units
            collideOnce:    False will make it collide with the unit every ANIMATION_PERIOD (~0.03 seconds)
            action:         The action to run once a target is hit
    */    
    function setCollisionFilter( ProjectileUnitFilter collisionFilter )
        this.collisionFilter = collisionFilter

    
    /**
        Add some code to run before each update of the projectile position.
    */
    function addPreUpdateAction( ProjectileAction action )
        this.action_preUpdate = action        
 
    function getPos() returns vec2
        return pos.toVec2()

    function getPos3() returns vec3
        return pos

    /** Disable this to make the projectile point in the same vertical direction regardless of the vertical velocity */
    function disablePitch()
        pitchEnabled = false


    /** Sets a custom update frequency for the projectile. The projectile will reach it's target once the distance it moves at
        an update is less than the remaining distance towards the target point.
        Example: if speed is 500, distance is 1200 and update freq is 1 second, it will only move twice. */
    function setUpdateFreq( real freq )
        updateFreq = (freq > 0) ? freq : 0


    /** NOT TESTED May behave strangley if used while a projectile is in flight */
    function setPos( vec3 pos )
        this.pos = pos
        projectileSfx.setPos(pos)


    /** Returns the sfx of the projectile, so you may rescale it, change it's color etc. */
    function getEffect() returns effect
        return projectileSfx


    /** Returns the target unit of the fireUnit() method */
    function getTargetUnit() returns unit
        return targetUnit


    /** Clears all unit's the projectile has collided with, such that it may collide with them again */
    function clearCollidedUnits()
        if collidedUnits != null
            collidedUnits.clear()

    
    /** Toggles the projectile to be destroyed or not once it's finished (reached it's target). Default is 'true'
        Toggle false, if you want to reuse the projectile once it's finished (for bounce i.e.). */
    function destroyOnFinish( boolean toggle )
        destroyOnFinish = toggle


//=============================================================================================================================================


public interface ProjectileAction
    function run()
    
public interface ProjectileUnitAction
    function run(unit target)
    
public interface ProjectileUnitFilter
    function filter(unit u) returns boolean
    
    